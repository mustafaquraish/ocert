import std::vec::{ Vec3f, Vec2f }
import std::value::{ Value }

import @objects::object::{ Object }
import @ray::{ Ray }
import @hit::{ Hit }
import @utils::{ vec_from_json, random_vec_in_unit_sphere }

struct Sphere {
    center: Vec3f
    radius: f32
}

def Sphere::new(center: Vec3f, radius: f32): &Object {
    let obj = Object::new(Sphere)
    obj.u.sphere.center = center
    obj.u.sphere.radius = radius
    return obj
}

def Sphere::intersect(&this, ray: &Ray, hit: &Hit): bool {
    let oc = ray.origin - this.center
    let a = ray.direction.dot(ray.direction)
    let b = 2.0 * oc.dot(ray.direction)
    let c = oc.dot(oc) - .radius * .radius
    let discriminant = b * b - 4.0 * a * c
    if discriminant < 0.0 {
        return false
    }

    let root = discriminant.sqrt()
    let t1 = (-b - root) / (2.0 * a)

    if t1 > ray.max_dist return false
    if t1 < 0.0001 then t1 = (-b + root) / (2.0 * a)
    if t1 < 0.0001 return false

    hit.distance = t1
    hit.position = ray.at(t1)
    let normal = (hit.position - .center) / .radius
    hit.normal = normal
    hit.uv.x = 0.5 + normal.z.atan2(normal.x) / std::math::TAU
    hit.uv.y = 0.5 + normal.y.asin() / std::math::PI
    ray.max_dist = t1
    return true
}

def Sphere::sample_point(&this, ray: &Ray, pdf: &f32): Vec3f {
    let res = random_vec_in_unit_sphere(ray)
    *pdf = 1.0 / (4.0 * std::math::PI * .radius * .radius)
    return .center + res * .radius
}

def Sphere::from_json(json: &Value): &Object {
    let center = if json.contains("center") {
        yield vec_from_json(json["center"])
    } else {
        yield Vec3f(0.0, 0.0, 0.0)
    }
    let radius = if json.contains("radius") {
        yield json["radius"].as_float() as f32
    } else {
        yield 1.0
    }
    return Sphere::new(center, radius)
}